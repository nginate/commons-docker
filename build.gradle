buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

plugins {
    id 'nebula.optional-base' version '3.1.0'
    id "com.jfrog.bintray" version "1.7.1"
    id 'net.researchgate.release' version '2.4.1'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'nebula.optional-base'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: "info.solidsoft.pitest"
apply from: 'gradle/distribution.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    projectUrl = 'https://github.com/nginate/commons-docker'
    vcsUrl = 'https://github.com/nginate/commons-docker.git'
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile "com.github.nginate:commons-lang:1.0.0"
    compile 'com.github.docker-java:docker-java:3.0.7'
    compile 'org.slf4j:slf4j-api:1.7.22'

    // Utils
    compile 'com.google.guava:guava:21.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.apache.commons:commons-collections4:4.1'

    // Annotation processors
    compile 'org.projectlombok:lombok:1.16.12', optional
    compile 'com.google.code.findbugs:jsr305:3.0.1', optional

    testCompile 'junit:junit:4.+'
    testCompile 'org.testng:testng:6.10.+'
    testCompile 'org.assertj:assertj-core:3.6.1'
    testCompile 'ch.qos.logback:logback-classic:1.1.+'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    mutateStaticInits = true
    timestampedReports = false
}

jacocoTestReport {
    group = "report"
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        stackTraceFilters = []
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
